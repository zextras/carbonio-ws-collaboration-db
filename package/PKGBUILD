pkgname="carbonio-ws-collaboration-db"
pkgver="0.2.0"
pkgrel="1"
pkgdesc="Carbonio Workstream Collaboration DB sidecar"
maintainer="Zextras <packages@zextras.com>"
url="https://zextras.com"
section="mail"
priority="optional"
arch=('x86_64')
license=("spdx:AGPL-3.0-only")
depends=(
  "service-discover"
  "pending-setups"
  "carbonio-core"
  "jq"
)
backup=(
  "etc/zextras/service-discover/carbonio-ws-collaboration-db.hcl"
)
source=(
  "carbonio-ws-collaboration-db"
  "carbonio-ws-collaboration-db.hcl"
  "carbonio-ws-collaboration-db.sh"
  "carbonio-ws-collaboration-db-bootstrap"
  "carbonio-ws-collaboration-db-sidecar.service"
  "intentions.json"
  "policies.json"
  "service-protocol.json"
)
sha256sums=('72692652bbb341b6f35a81171063b2bbf9780b60ae91c266575aaa4dde255ac7'
  'f5174863bfbefd426ef67ee2c545cdbe534d8a89bde94d7cdc891895935adf77'
  '2c175f681ebb7cbac3f8f034ffe08d99d59b8a68824ebcfce1877c22614efd66'
  '3cd08503c4acb0cddd8104e8c4cccadb9837fa7028c4c2dd85841c34f327f7fb'
  '2e37b88a45d0b99cf86e7bd17721f030c23c2c692ab648b6a645f05725dbf642'
  'adf29d0330cd8745c81a03eb27feea87f13a11f09e9cba2bebc10e0bb9bd4236'
  '9bedfb38742c72034f015aee3f1e7bdb734cd5d6e0003b109a8d6fead4962a8c'
  'd0f4019f14ff13fe5d394e9945c6679c1098f532c8bffd41b129e1a804947ac1')

package() {
  cd "${srcdir}"
  install -Dm 755 carbonio-ws-collaboration-db "${pkgdir}/usr/bin/carbonio-ws-collaboration-db"
  install -Dm 644 carbonio-ws-collaboration-db-sidecar.service "${pkgdir}/lib/systemd/system/carbonio-ws-collaboration-db-sidecar.service"
  install -Dm 644 carbonio-ws-collaboration-db.hcl "${pkgdir}/etc/zextras/service-discover/carbonio-ws-collaboration-db.hcl"
  install -Dm 755 carbonio-ws-collaboration-db-bootstrap "${pkgdir}/usr/bin/carbonio-ws-collaboration-db-bootstrap"
  install -Dm 644 carbonio-ws-collaboration-db.sh "${pkgdir}/etc/zextras/pending-setups.d/carbonio-ws-collaboration-db-setup.sh"
  install -Dm 644 intentions.json "${pkgdir}/etc/carbonio/ws-collaboration-db/service-discover/intentions.json"
  install -Dm 644 policies.json "${pkgdir}/etc/carbonio/ws-collaboration-db/service-discover/policies.json"
  install -Dm 644 service-protocol.json "${pkgdir}/etc/carbonio/ws-collaboration-db/service-discover/service-protocol.json"
}

postinst() {
  getent group 'carbonio-ws-collaboration-db' >/dev/null ||
    groupadd -r 'carbonio-ws-collaboration-db'
  getent passwd 'carbonio-ws-collaboration-db' >/dev/null ||
    useradd -r -M -g 'carbonio-ws-collaboration-db' -s /sbin/nologin 'carbonio-ws-collaboration-db'

  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
    systemctl enable carbonio-ws-collaboration-db-sidecar.service >/dev/null 2>&1 || :
  fi

  echo "============================================================"
  echo "Carbonio Workstream Collaboration DB installed successfully!"
  echo "You must run pending-setups to configure it correctly.      "
  echo "============================================================"
}

prerm() {
  if [ -d /run/systemd/system ]; then
    systemctl --no-reload disable carbonio-ws-collaboration-db-sidecar.service >/dev/null 2>&1 || :
    systemctl stop carbonio-ws-collaboration-db-sidecar.service >/dev/null 2>&1 || :
  fi
}

postrm() {
  rm -f /etc/carbonio/ws-collaboration/service-discover/token
  if [ -d /run/systemd/system ]; then
    systemctl daemon-reload >/dev/null 2>&1 || :
  fi
}
